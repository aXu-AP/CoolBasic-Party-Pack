//Pelaajien hallinta

//Pelaajan muistipalan offsettej‰
Const MAIN_PLAYER_NAME = 0      //Viittaa merkkijonoon, joka sis‰lt‰‰ pelaajan nimen
Const MAIN_PLAYER_COLOR = 4     //Viittaa muistipalaan, joka sis‰lt‰‰ pelaajan v‰rin
Const MAIN_PLAYER_CONTROLS = 8  //Viittaa muistipalaan, joka sis‰lt‰‰ pelaajan n‰pp‰imet
Const MAIN_PLAYER_DYNAMIC = 12  //Viittaa muistipalaan, joka on tarkoitettu minipelikohtaista tiedontallennusta varten
Const MAIN_PLAYER_POINTS = 0   //Sis‰lt‰‰ pelaajan pisteet (Int)

//Pelaajan muistipalan koko
Const MAIN_PLAYER_STATIC_SIZE = 16

//Pelaajien maksimim‰‰r‰ 8, tietokoneelle harvoin pystyy sen enemp‰‰ pelaamaan (loppuu n‰pp‰imet ja kapulat :D )
Const MAIN_MAX_PLAYERS = 8

//Sis‰lt‰‰ viittaukset pelaajien muistipaloihin
Dim MAIN_Player(MAIN_MAX_PLAYERS) As integer 

//Sis‰lt‰‰ todellisen tiedon pelaajien m‰‰r‰st‰ (minipelille saatetaan ilmoittaa eri m‰‰r‰)
Global MAIN_PlayerAmount
MAIN_PlayerAmount = 0


Type MAIN_PlayerString
    Field id
	Field txt$
EndType


//Lis‰‰ pelaajan ja v‰rin, palauttaa pelaajan numeron
Function MAIN_AddPlayer(name$, r = -1, g = -1, b = -1)
	number = -1
	
	//Luodaan pelaaja seuraavalle vapaalle indeksille jos on olemassa (muutoin palautetaan -1)
	For i = 0 To MAIN_MAX_PLAYERS - 1
		If (MAIN_Player(i) = 0) Then number = i : Exit
	Next i
	
	If (number <> -1) Then 
		MAIN_PlayerAmount = MAIN_PlayerAmount + 1
		
		player = MakeMEMBlock(MAIN_PLAYER_STATIC_SIZE)
		
		iS. MAIN_PlayerString = New(MAIN_PlayerString)
		iS\txt$ = name$
		iS\id = ConvertToInteger(iS)  

		col = MakeMEMBlock(4)
		ctrl = MakeMEMBlock(API_ACTIONS + 1)
		
		PokeInt player, MAIN_PLAYER_NAME, iS\id
		PokeInt player, MAIN_PLAYER_COLOR, col
		PokeInt player, MAIN_PLAYER_CONTROLS, ctrl
		PokeInt player, MAIN_PLAYER_DYNAMIC, MakeMEMBlock(128)
		
		MAIN_Player(number) = player

		API_SetPlayerColor(number,r,g,b)
	EndIf 
	
	Return number
EndFunction 

// Asettaa ja palauttaa pelaajan pisteet
Function API_PlayerPoints(number, points = -1)
	If (number >= 0 And number < MAIN_MAX_PLAYERS) Then
	
		player = MAIN_Player(number)
		
		dynamic = PeekInt(player, MAIN_PLAYER_DYNAMIC)
		
		If (points >= 0) Then 
			PokeInt dynamic, MAIN_PLAYER_POINTS, points
			Return points  
		Else 
			Return peekint(dynamic, MAIN_PLAYER_POINTS)
		EndIf 

	EndIf 
	
	Return 0
EndFunction 

Function API_SetPlayerColor(number, r = -1, g = -1, b = -1)
	If (number >= 0 And number < MAIN_MAX_PLAYERS) Then
		player = MAIN_Player(number)
		
		col = PeekInt(player, MAIN_PLAYER_COLOR)
	
		If (r < 0 Or g < 0 Or b < 0 Or r > 255 Or g > 255 Or b > 255) Then 
			c = Rand(1, 6)
			c = c Shl 29
			PokeByte col, RED, 127 + 127 * (c Shr 31) 
			
			c = c Shl 1
			PokeByte col, GREEN, 127 + 127 * (c Shr 31) 
			
			c = c Shl 1
			PokeByte col, BLUE, 127 + 127 * (c Shr 31) 
		Else 
			PokeByte col, RED, r
			PokeByte col, GREEN, g
			PokeByte col, BLUE, b
		EndIf   
	EndIf 
EndFunction 

//Kopioi staatisen ja/tai dynaamisen muistipalan
//pelaajalle jos annettu pala on erisuuri kuin nolla
Function MAIN_SetPlayerData(number, static = 0, dynamic = 0)
	If (number >= 0 And number < MAIN_MAX_PLAYERS) Then
		player = MAIN_Player(number) 
		
		If (static <> 0 And MEMBlockSize(static) = MAIN_PLAYER_STATIC_SIZE) Then 
			MemCopy static, 0, player, 0, MAIN_PLAYER_STATIC_SIZE
		EndIf 
		
		p_dynamic = PeekInt(player, MAIN_PLAYER_DYNAMIC)
		
		If (dynamic <> 0) Then
			size = MEMBlockSize(dynamic)
			ResizeMEMBlock p_dynamic, size
			MemCopy dynamic, 0, p_dynamic, 0, size
			PokeInt player, MAIN_PLAYER_DYNAMIC, p_dynamic
		EndIf 
	EndIf 
EndFunction 

//Lataa defaulttiasetukset 
//TODO: mahdollista n‰pp‰inten lataus tiedostosta, 
//mieluiten Gamesettinginsin kautta
Function MAIN_LoadKeys()
	If (MAIN_Player(0) <> 0) Then
		player = MAIN_Player(0)
		
		ctrl = PeekInt(player, MAIN_PLAYER_CONTROLS)
		
		PokeByte ctrl, API_UP, 		200 //Ylˆs nuoli
		PokeByte ctrl, API_DOWN, 	208 //Alas nuoli
		PokeByte ctrl, API_LEFT, 	203 //Vasen nuoli
		PokeByte ctrl, API_RIGHT, 	205 //Oikea nuoli
		PokeByte ctrl, API_ACTION1, 54  //Oikea shift
		PokeByte ctrl, API_ACTION2, 221 //Oikea ctrl
		PokeByte ctrl, API_CONTROL_SOURCE, API_KEYBOARD
	EndIf 
		
	If (MAIN_Player(1) <> 0) Then
		player = MAIN_Player(1)
		
		ctrl = PeekInt(player, MAIN_PLAYER_CONTROLS)
		
		PokeByte ctrl, API_UP, 		17 //W
		PokeByte ctrl, API_DOWN,	31 //S
		PokeByte ctrl, API_LEFT, 	30 //A
		PokeByte ctrl, API_RIGHT, 	32 //D
		PokeByte ctrl, API_ACTION1, 42 //Vasen shift
		PokeByte ctrl, API_ACTION2, 86 //<>
		PokeByte ctrl, API_CONTROL_SOURCE, API_KEYBOARD
	EndIf 
End Function
