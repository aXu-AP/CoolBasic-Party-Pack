//Kutakuinkin jokainen minipeli pit‰‰ kirjaa pelaajien pisteist‰. Ettei jokaiseen minipeliin tarvitse tehd‰ omaa taulukkoa/muistipalaa tai
//muuta tapaa tallentaa v‰liaikaispisteit‰, luodaan kaikille peleille yhteinen taulukko.
Dim API_PlayerScore(3)                          //T‰h‰n tallennetaan pelaajien v‰liaikaiset pisteet

// T‰st‰ taulukosta lˆytyy pelaajien v‰rit
// voidaan k‰ytt‰‰ CoolBasicin omia RED, GREEN ja BLUE vakioita
// komponettien erottamiseen. Ensimm‰inen numero on pelaajan numero
// Esim. API_PlayerColor(2,RED)	 

Dim API_PlayerColor(3,3) As Byte 		


Function API_DrawScreen()                       //Korvaa DrawScreen
	DrawScreen
End Function

//V‰rj‰‰ kuvan pelaajan tunnusv‰rill‰
//V‰ri haetaan API_PlayerColor taulukosta
//Erityisesti harmaas‰vyisille kuville
//Palauttaa kuvan 
Function API_Recolor(img, playerNumber)

	//Pelaajan v‰rin suhteellinen voimakkuus 0.0 - 1.0
	redAmount#   = Float(API_PlayerColor(playerNumber,RED))   / 255.0
	greenAmount# = Float(API_PlayerColor(playerNumber,GREEN)) / 255.0
	blueAmount#  = Float(API_PlayerColor(playerNumber,BLUE))  / 255.0

	//lukittu kuva ett‰ saadaan nopeutta
	Lock Image(img)
		For x=0 To ImageWidth(img)-1
			For y=0 To ImageHeight(img)-1
			
				//pikselista RGB
				pixel = GetPixel2 (x,y,Image(img))
				
				pixel = pixel Shl 8
				r = pixel Shr 24
				
				pixel = pixel Shl 8
				g = pixel Shr 24
				
				pixel = pixel Shl 8
				b = pixel Shr 24 
				
				// kerrotaan kukin pikseliarvo suhteellisella voimakkuudella
				r = Float(r) * redAmount# 
				g = Float(g) * greenAmount# 
				b = Float(b) * blueAmount# 
				
				//RGB:st‰ pikseli
				p = b + (g Shl 8) + (r Shl 16) + (255 Shl 24)
				
				PutPixel2 x,y,p,Image(img)
				
			Next y
		Next x
	Unlock Image(img)
    
    Return img
End Function 
